//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Blow Job! XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

// Decompiled from "ste_re1.scr" Using Epic GTA2 Script Decompiler by T.M.

//FORWARD redn_easy_phone1_m1:
//FORWARD clean_up_redn_easy_phone1_m1:

CAR_DATA r_e_1_pickup_car

COUNTER r_e_1_is_player_in_pickup = 0
COUNTER r_e_1_is_pickup_in_position = 0
COUNTER r_e_1_mission_stage = 0
COUNTER r_e_1_bomb_brief_flag = 0

CHAR_DATA r_e_1_trailer_guy

COUNTER r_e_1_pickup_rigged_bomb = 0               //  = 1 means pickup rigged with a bomb

OBJ_DATA r_e_1_shotgun                  //  Shotgun

redn_easy_phone1_m1:

	SET flag_failed_redn_easy_phone1_m1 = 0
 
	DISPLAY_MESSAGE (2671)             //  BLOW JOB!
	DISPLAY_BRIEF (2001)               //  "Hey, Rooster, it's Billy Bob Bean. Montana has wired some new explosives to the ass of a Pickup and I want you to collect it."
 
	r_e_1_pickup_car = CREATE_GANG_CAR (24.00, 41.50, 2.00) 3 90 PICKUP END
	POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
 
	SET flag_on_redn_mission = 1
	SET flag_on_mission = 1
 
 //  If the trailer hasn't exploded yet  (in practice, this is a mission checkpoint checker)
 IF ( flag_r_e_1_checkpoint = 0 )
    
    WHILE_EXEC ( r_e_1_mission_stage = 0 )
       
       //  If the car was unduly destroyed
       IF ( ( IS_CAR_WRECKED(r_e_1_pickup_car) ) AND ( flag_failed_redn_easy_phone1_m1 = 0 ) )
          CLEAR_ALL_BRIEFS ()
	         DISPLAY_BRIEF_NOW (2002)          //  "I told you to collect the Pickup, Rooster, not blow it to smithereens."
	         SET flag_failed_redn_easy_phone1_m1 = 1
	         DISPLAY_MESSAGE (2998)            //  Mission Failed!
          RETURN
       ENDIF
       
       //  If the player is in the pickup, give it a bomb
       IF ( ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) AND ( r_e_1_pickup_rigged_bomb = 0 ) )
          GIVE_WEAPON (p1, CAR_BOMB)
          SET r_e_1_pickup_rigged_bomb = 1
       ENDIF
       
       //  The player picked the pickup
       IF ( ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) AND ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) ) )
          IF ( r_e_1_is_player_in_pickup = 0 )
             SET r_e_1_is_player_in_pickup = 1
             DISPLAY_BRIEF_NOW (2003)             //  "Fat Boy Jim has hauled his crusty ass out bowling. Park the rigged Pickup by his Trailer and we can test the explosives."
             POINT_ARROW_AT (arrow1, 48.50, 20.50, 2.00)
          ENDIF
       ENDIF
       
       //  If the player leaves the pickup away from the bomb point
       IF ( ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) ) AND ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) ) )
          IF ( r_e_1_is_player_in_pickup = 1 )
             SET r_e_1_is_player_in_pickup = 0
             DISPLAY_BRIEF_NOW (2007)             //  "This ain't joyridin', Rooster. Get back in the Pickup!"
             POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
          ENDIF
       ENDIF
       
       //  The player reached at bomb point
       IF ( ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) AND ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) )
          IF ( r_e_1_is_pickup_in_position = 0 )
             REMOVE_ARROW (arrow1)
             SET r_e_1_is_pickup_in_position = 1
             IF ( r_e_1_bomb_brief_flag = 0 )
                DISPLAY_BRIEF_NOW (2005)          //  "Arm the explosives with your trigger finger, Rooster, but don't hang around."
                SET r_e_1_bomb_brief_flag = 1
             ENDIF
          ENDIF
       ENDIF
       
       //  If the player get away from the bomb point after reaching it
       IF ( ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) ) AND ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) )
          IF ( r_e_1_is_pickup_in_position = 1 )
             POINT_ARROW_AT (arrow1, 48.50, 20.50, 2.00)
             DISPLAY_BRIEF_NOW (2004)              //  "Get back to Fat Boy Jim's trailer, Rooster."
             SET r_e_1_is_pickup_in_position = 0
          ENDIF
       ENDIF
       
       //  The player activated the bomb at the right place
       IF ( ( IS_CARBOMB_ACTIVE(r_e_1_pickup_car) ) AND ( r_e_1_is_pickup_in_position = 1 ) )
          DISPLAY_BRIEF_NOW (2006)                 //  "The clock's tickin'! Get ya ass outta the Pickup, Rooster - she's gonna blow!"
          SET r_e_1_mission_stage = 1
          SET r_e_1_is_pickup_in_position = 2
          REMOVE_ARROW (arrow1)
       ENDIF
       
       //  The player leaves the pickup in the bomb point, but forgot to activate it
       IF ( ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) AND ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) ) )
          IF ( r_e_1_is_pickup_in_position = 1 )
             POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
             DISPLAY_BRIEF_NOW (2008)              //  "Them explosives have to be armed, Rooster. Get back in the Pickup and trigger the bomb!"
             SET r_e_1_is_pickup_in_position = 0
          ENDIF
       ENDIF
    
    ENDWHILE
    
 ENDIF
 
 //  The player has activated the bomb at the right place
 
 //  Checking again if the trailer hasn't exploded yet
 IF ( flag_r_e_1_checkpoint = 0 )
 
    WHILE_EXEC ( r_e_1_mission_stage = 1 )
       
       //  The car is wrecked but away from the trailer
       IF ( ( IS_CAR_WRECKED(r_e_1_pickup_car) ) AND ( NOT ( CHECK_CAR_WRECKED_IN_AREA(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) ) )
          IF ( flag_failed_redn_easy_phone1_m1 = 0 )
             CLEAR_ALL_BRIEFS ()
             DISPLAY_BRIEF_NOW (2010)       //  "I weren't intendin' on destroyin' the whole damn Trailer Park, Rooster!"
             SET flag_failed_redn_easy_phone1_m1 = 1
             DISPLAY_MESSAGE (2998)         //  Mission Failed!
             RETURN
          ENDIF
       ENDIF
       
       //  The car is wrecked beside the trailer?
       IF ( ( IS_CAR_WRECKED(r_e_1_pickup_car) ) AND ( CHECK_CAR_WRECKED_IN_AREA(r_e_1_pickup_car, 48.50, 20.50, 2.00, 3.00, 1.00) ) )
          SET r_e_1_mission_stage = 2
       ENDIF
    
    ENDWHILE
    
    //  The car is wrecked beside the trailer   (you can explode it before the bomb activates using a rocket laucher, it will speed up the mission a little)
    
    //  BOOOOM!!
    
    EXEC
    
        EXPLODE (47.00, 19.00, 4.00)
        EXPLODE (48.00, 19.30, 4.00)
        DELAY_HERE (1)
        EXPLODE (49.00, 19.60, 4.00)
        DELAY_HERE (1)
        EXPLODE (51.00, 19.50, 4.00)
        EXPLODE (47.20, 19.00, 4.00)
        DELAY_HERE (1)
        EXPLODE (48.40, 19.30, 4.00)
        EXPLODE (49.10, 19.20, 4.00)
        DELAY_HERE (2)
        REMOVE_BLOCK (47, 19, 2, DO_DROP)
        REMOVE_BLOCK (48, 19, 2, DO_DROP)
        REMOVE_BLOCK (49, 19, 2, DO_DROP)
        REMOVE_BLOCK (51, 19, 2, DO_DROP)
    
    ENDEXEC
    
    //  Create the guy in the trailer and explode it, so he will be thrown by the explosion
    r_e_1_shotgun = CREATE_OBJ (47.50, 19.50, 2.00) 0 COLLECT_06 END
    r_e_1_trailer_guy = CREATE_CHAR (48.50, 19.50, 2.00) 5 180 DUMMY END
    EXPLODE (r_e_1_trailer_guy)
 
 ENDIF
 
 //  Finished the first part of the mission
 
 
 EXEC
     
     //  The trailer has been exploded, set a mission checkpoint
     SET flag_r_e_1_checkpoint = 1
     
     //  Recycling the counters
     SET r_e_1_is_player_in_pickup = 0
     SET r_e_1_is_pickup_in_position = 0
     SET r_e_1_mission_stage = 0
     SET r_e_1_bomb_brief_flag = 0
     SET r_e_1_pickup_rigged_bomb = 0

     DISPLAY_BRIEF_NOW (2012)             //  "This is makin' progress, Rooster. Montana has another Pickup rigged to do some damage. Go 'n' get it."
     
     //  Another pickup
     r_e_1_pickup_car = CREATE_GANG_CAR (24.00, 44.50, 2.00) 3 90 PICKUP END
     POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
     
     //  Ensuring that all mission flags are on
     SET flag_on_redn_mission = 1
     SET flag_on_mission = 1
 
 ENDEXEC
 
 
 
 WHILE_EXEC ( r_e_1_mission_stage = 0 )
    
    //  If the car was unduly destroyed
    IF ( IS_CAR_WRECKED(r_e_1_pickup_car) )
       IF ( flag_failed_redn_easy_phone1_m1 = 0 )
          CLEAR_ALL_BRIEFS ()
          DISPLAY_BRIEF_NOW (2013)        //  "You ain't too bright, Rooster. Exploding y'self is kinda dumb."
          SET flag_failed_redn_easy_phone1_m1 = 1
          DISPLAY_MESSAGE (2998)          //  Mission Failed!
          RETURN
       ENDIF
    ENDIF
    
    //  If the player is in the pickup, give it a bomb
    IF ( ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) AND ( r_e_1_pickup_rigged_bomb = 0 ) )
       GIVE_WEAPON (p1, CAR_BOMB)
       SET r_e_1_pickup_rigged_bomb = 1
    ENDIF
    
    //  The player picked the pickup
    IF ( ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) AND ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) ) )
       IF ( r_e_1_is_player_in_pickup = 0 )
          SET r_e_1_is_player_in_pickup = 1
          DISPLAY_BRIEF_NOW (2014)          //  "The Zaibatsu have barricaded the key access point to their territory. Gettin' rid o' the barricade could be helpful in the future."
          POINT_ARROW_AT (arrow1, 110.50, 77.50, 2.00)
       ENDIF
    ENDIF
    
    //  If the player leaves the pickup away from the bomb point
    IF ( ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) ) AND ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) ) )
       IF ( r_e_1_is_player_in_pickup = 1 )
          SET r_e_1_is_player_in_pickup = 0
          DISPLAY_BRIEF_NOW (2015)          //  "Get your ass back in that wired vehicle, boy!"
          POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
       ENDIF
    ENDIF
    
    //  The player reached at bomb point
    IF ( ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) AND ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) )
       IF ( r_e_1_is_pickup_in_position = 0 )
          REMOVE_ARROW (arrow1)
          SET r_e_1_is_pickup_in_position = 1
          
          IF ( r_e_1_bomb_brief_flag = 0 )
             DISPLAY_BRIEF_NOW (2016)       //  "Arm the explosives with your trigger finger, Rooster!"
             SET r_e_1_bomb_brief_flag = 1
          ENDIF
          
       ENDIF
    ENDIF
    
    //  If the player get away from the bomb point after reaching it
    IF ( ( NOT ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) ) AND ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) )
       IF ( r_e_1_is_pickup_in_position = 1 )
          POINT_ARROW_AT (arrow1, 111.50, 77.50, 2.00)
          DISPLAY_BRIEF_NOW (2017)                       //  "That was the right place, Rooster. Get back and park the damn Pickup!"
          SET r_e_1_is_pickup_in_position = 0
       ENDIF
    ENDIF
    
    //  The player activated the bomb at the right place
    IF ( IS_CARBOMB_ACTIVE(r_e_1_pickup_car) )
       IF ( r_e_1_is_pickup_in_position = 1 )
          DISPLAY_BRIEF_NOW (2018)                       //  "Get out of the Pickup - she's gonna blow!"
          SET r_e_1_mission_stage = 1
          SET r_e_1_is_pickup_in_position = 2
          REMOVE_ARROW (arrow1)
       ENDIF
    ENDIF
    
    //  The player leaves the pickup in the bomb point, but forgot to activate it
    IF ( ( IS_CAR_IN_BLOCK(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) AND ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_1_pickup_car) ) ) )
       IF ( r_e_1_is_pickup_in_position = 1 )
          POINT_ARROW_AT (arrow1, r_e_1_pickup_car)
          DISPLAY_BRIEF_NOW (2019)                 //  "Arm the explosives inside the Pickup, Rooster!"
          SET r_e_1_is_pickup_in_position = 0
       ENDIF
    ENDIF
 
 ENDWHILE
 
 //  The player has activated the bomb at the zaibatsu barricade
 
 WHILE_EXEC ( r_e_1_mission_stage = 1 )
    
    //  The car is wrecked but away from the zaibatsu barricade
    IF ( ( IS_CAR_WRECKED(r_e_1_pickup_car) ) AND ( NOT ( CHECK_CAR_WRECKED_IN_AREA(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) ) )
       IF ( flag_failed_redn_easy_phone1_m1 = 0 )
          CLEAR_ALL_BRIEFS ()
          DISPLAY_BRIEF_NOW (2020)                  //  "Just exactly where did you learn to blow up absolutely nothin', Rooster? You find yourself another job!"
          SET flag_failed_redn_easy_phone1_m1 = 1
          DISPLAY_MESSAGE (2998)                    //  Mission Failed!
          RETURN
       ENDIF
    ENDIF
    
    //  The car is wrecked beside the zaibatsu barricade?
    IF ( ( IS_CAR_WRECKED(r_e_1_pickup_car) ) AND ( CHECK_CAR_WRECKED_IN_AREA(r_e_1_pickup_car, 111.50, 77.50, 2.00, 3.00, 1.00) ) )
       SET r_e_1_mission_stage = 2
    ENDIF
 
 ENDWHILE
 
 //  The pickup blow up at the right position
 
 
 //  BOOOOM!!
 
 EXEC
 
     EXPLODE (111.50, 75.50, 4.00)
     EXPLODE (111.50, 76.50, 4.00)
     DELAY_HERE (1)
     EXPLODE (111.50, 77.50, 4.00)
     DELAY_HERE (1)
     EXPLODE (111.50, 78.50, 4.00)
     EXPLODE (111.50, 77.50, 4.00)
     DELAY_HERE (1)
     EXPLODE (111.50, 75.50, 4.00)
     EXPLODE (111.50, 76.50, 4.00)
     DELAY_HERE (2)
     REMOVE_BLOCK (111, 75, 2, DO_DROP)
     REMOVE_BLOCK (111, 76, 2, DO_DROP)
     REMOVE_BLOCK (111, 77, 2, DO_DROP)
     REMOVE_BLOCK (111, 78, 2, DO_DROP)
     
 ENDEXEC
 
 //  Mission Finished

 CLEAR_ALL_BRIEFS ()
 DISPLAY_BRIEF_NOW (2021)      //  "Yee-ha! Them Zaibatsu don't know what hit 'em! Here's $30,000. [...]"

 DELAY_HERE (2)

 ADD_SCORE (p1, 30000)
 
 SET flag_passed_redn_easy_phone1_m1 = 1
 
 DISPLAY_MESSAGE (2999)        //  Mission Complete!
 
 CLEAR_WANTED_LEVEL (p1)
 ADD_MULTIPLIER (p1, 1)
 
 CHANGE_GANG_CHAR_RESPECT_AND_UPDATE (redngang, p1, 1)    //  RESPECT +
 
 SET_PHONE_DEAD (ph_redn_easy_phone1)                   //  Kill the Redneck Easy Phone 1
 DISABLE_THREAD_TRIGGER (thr_redn_easy_phone1_m1)       //  Disable Redneck Easy Phone 1 trigger
 
RETURN



clean_up_redn_easy_phone1_m1:

	SET flag_on_mission = 0
	SET flag_on_redn_mission = 0
 
	REMOVE_ARROW (arrow1)
 
 IF ( flag_passed_redn_easy_phone1_m1 = 1 )
    ++flag_all_missions_passed
	   ++flag_redn_missions_passed
	ENDIF
 
 IF ( CHECK_DEATHARREST_EXECUTED() )
	   ++flag_redn_missions_failed
	   ++flag_all_missions_failed
	   CHANGE_GANG_CHAR_RESPECT_AND_UPDATE (redngang, p1, -1)        //  Lose Respect
	   CLEAR_WANTED_LEVEL (p1)
 ENDIF
 
	MISSION_HAS_FINISHED ()
 
RETURN



MISSIONSTART
GOSUB redn_easy_phone1_m1:
GOSUB clean_up_redn_easy_phone1_m1:
MISSIONEND
