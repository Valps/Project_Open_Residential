//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Double-Cross Crush! XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
//XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

// Decompiled from "ste_re2.scr" Using Epic GTA2 Script Decompiler by T.M.

//FORWARD redn_easy_phone2_m1:
//FORWARD clean_up_redn_easy_phone2_m1:

CAR_DATA r_e_2_ztype_car

COUNTER r_e_2_player_in_meteor = 0
COUNTER r_e_2_while_1_loop = 0
COUNTER r_e_2_scie_bonus_started = 0

CAR_DATA r_e_2_srs_meteor_car

COUNTER r_e_2_player_in_ztype = 0
COUNTER r_e_2_while_2_loop = 0
COUNTER r_e_2_zaib_bonus_started = 0
COUNTER r_e_2_hunt_started = 0

ONSCREEN_COUNTER r_e_2_scie_onscreen_counter
ONSCREEN_COUNTER r_e_2_zaib_onscreen_counter

COUNTER r_e_2_scie_yet_to_kill = 0
COUNTER r_e_2_zaib_yet_to_kill = 0
COUNTER r_e_2_kills_goal = 50          //  Gang members to be killed  (if you want to change it, you need change both START_BONUS_CHECK and both r_e_2_...._yet_to_kill counters as well)
COUNTER r_e_2_scientists_killed = 0
COUNTER r_e_2_zaibatsu_killed = 0
COUNTER r_e_2_player_fit_conditions = 0          //  = 1 if Player is using the correct car and at the correct gang zone, 0 otherwise

redn_easy_phone2_m1:

	SET flag_failed_redn_easy_phone2_m1 = 0
 
	DISPLAY_MESSAGE (2672)          //  DOUBLE-CROSS CRUSH!
 
	SET flag_on_mission = 1
	SET flag_on_redn_mission = 1
 
	DISPLAY_BRIEF_NOW (2022)        //  "Hey, Rooster! Billy Bob Bean here. I'm real pissed with the Zaibatsu [...] Go steal a Zaibatsu Z-Type."
 
 WHILE_EXEC ( r_e_2_while_1_loop = 0 )
    
    //  The player entered a Z-Type
    IF ( ( r_e_2_player_in_ztype = 0 ) AND ( IS_CHARACTER_IN_MODEL(p1, VTYPE) ) )
    
       STORE_CAR_CHARACTER_IS_IN (p1, r_e_2_ztype_car)
	      POINT_ARROW_AT (arrow1, 227.50, 217.50, 2.00)             //  Point arrow at Scientist's gang zone
	      SET r_e_2_player_in_ztype = 1
       
       //  If the player isn't in scientist's gang zone
       IF ( NOT ( IS_CHAR_IN_GANGZONE(p1, sciegang) ) )
          DISPLAY_BRIEF_NOW (2023)                               //  "Right on! Drive over to the SRS area and start killin' Scientists!"
       ENDIF
       
    ENDIF
    
    //  If the player leaves his Z-Type
    IF ( ( r_e_2_player_in_ztype = 1 ) AND ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_2_ztype_car) ) ) )
       SET r_e_2_player_in_ztype = 0
	      DISPLAY_BRIEF_NOW (2024)                             //  "You ain't got a Z-Type, Rooster! Go get one!"
	      REMOVE_ARROW (arrow1)
    ENDIF
    
    //  If the Z-Type has been destroyed
    IF ( ( r_e_2_player_in_ztype = 1 ) AND ( IS_CAR_WRECKED(r_e_2_ztype_car) ) )
       IF ( flag_failed_redn_easy_phone2_m1 = 0 )
          CLEAR_ALL_BRIEFS ()
	         DISPLAY_BRIEF_NOW (2025)                          //  "What you playin' at, Rooster? That Z-Type is good fer nothin' now."
	         SET flag_failed_redn_easy_phone2_m1 = 1
	         DISPLAY_MESSAGE (2998)                            //  Mission Failed!
          RETURN
       ENDIF
    ENDIF
    
    //  If the player entered the scientist's gang zone driving a Z-Type
    IF ( ( IS_CHAR_IN_GANGZONE(p1, sciegang) ) AND ( r_e_2_player_in_ztype = 1 ) )
       IF ( r_e_2_scie_bonus_started = 0 )
          DISPLAY_BRIEF_NOW (2026)                          //  "Time fer killin', Rooster! Run down 50 Scientists with the Zaibatsu Z-Type."
          r_e_2_kill_scientists_bonus = START_BONUS_CHECK (sciegang, 65535, 50, 0, CHAR, NOT_EXCLUSIVE, BY_VEHICLE, VTYPE, sciegang, 7)       //  65535 = NONE? or -1?
          SET r_e_2_scie_yet_to_kill = 50
          SET r_e_2_scie_bonus_started = 1                        //  Lock this IF
          SET r_e_2_hunt_started = 1
          SET r_e_2_player_fit_conditions = 1
          ADD_ONSCREEN_COUNTER (r_e_2_scie_onscreen_counter, r_e_2_scie_yet_to_kill)
          REMOVE_ARROW (arrow1)
       ENDIF
    ENDIF
    
    //  If the player leaves the scientist's gang zone after triggering the bonus counter
    IF ( ( NOT ( IS_CHAR_IN_GANGZONE(p1, sciegang) ) ) AND ( r_e_2_scie_bonus_started = 1 ) )
       IF ( r_e_2_player_fit_conditions = 1 )
          DISPLAY_BRIEF_NOW (2027)                            //  "Feelin' yella, Rooster? Get back to the SRS Compound"
          POINT_ARROW_AT (arrow1, 227.50, 217.50, 2.00)       //  Point arrow back to scientist's gang zone
          SET r_e_2_player_fit_conditions = 0
       ENDIF
    ENDIF
    
    //  If the player is in scientist's gang zone while doing the bonus, remove the arrow and update flag
    IF ( ( IS_CHAR_IN_GANGZONE(p1, sciegang) ) AND ( r_e_2_scie_bonus_started = 1 ) )
       REMOVE_ARROW (arrow1)
       SET r_e_2_player_fit_conditions = 1
    ENDIF
    
    //  If the hunt has began, store the number of scientists killed in counter12
    IF ( r_e_2_hunt_started = 1 )
       STORE_BONUS_COUNT (r_e_2_kill_scientists_bonus, r_e_2_scientists_killed)
    ENDIF
    
    //  If the hunt has began, update the onscreen counter (the number that appears on player screen)
    IF ( r_e_2_hunt_started = 1 )
       SET r_e_2_scie_yet_to_kill = (r_e_2_kills_goal - r_e_2_scientists_killed)            //  The number of scientists yet to be killed
    ENDIF
    
    //  If the player killed 50 scientists
    IF ( ( r_e_2_scie_bonus_started = 1 ) AND ( HAS_BONUS_PASSED(r_e_2_kill_scientists_bonus) ) )
       CLEAR_ALL_BRIEFS ()
       DISPLAY_BRIEF_NOW (2028)                        //  "You did it, Rooster! Now steal an SRS Meteor and drive into Zaibatsu Village."
       CLEAR_ONSCREEN_COUNTER (r_e_2_scie_onscreen_counter)
       SET r_e_2_while_1_loop = 1
       SET r_e_2_hunt_started = 0
    ENDIF
    
    //  If the bonus failed          ( how? I don't know )
    IF ( ( r_e_2_scie_bonus_started = 1 ) AND ( HAS_BONUS_FAILED(r_e_2_kill_scientists_bonus) ) )
       CLEAR_ALL_BRIEFS ()
       DISPLAY_BRIEF_NOW (2037)                        //  "There ain't no time limit, boy, but I'm getting' sober waitin' here!"
       CLEAR_ONSCREEN_COUNTER (r_e_2_scie_onscreen_counter)
       SET flag_failed_redn_easy_phone2_m1 = 1
       DISPLAY_MESSAGE (2998)
       RETURN
    ENDIF
 
 ENDWHILE
 
 //  The player passed the bonus and killed 50 scientists
 
 WHILE_EXEC ( r_e_2_while_2_loop = 0 )
    
    //  The player entered a SRS Meteor
    IF ( ( r_e_2_player_in_meteor = 0 ) AND ( IS_CHARACTER_IN_MODEL(p1, STRATOSB) ) )
    
       STORE_CAR_CHARACTER_IS_IN (p1, r_e_2_srs_meteor_car)
       POINT_ARROW_AT (arrow1, 199.00, 25.50, 3.00)        //  Point arrow at Zaibatsu's gang zone
       SET r_e_2_player_in_meteor = 1
       
       //  If the player isn't in zaibatsu's gang zone
       IF ( NOT ( IS_CHAR_IN_GANGZONE(p1, sciegang) ) )
          DISPLAY_BRIEF_NOW (2030)                         //  "Get on down to Zaibatsu Village and start killin' Zaibatsu!"
       ENDIF
       
    ENDIF
    
    //  If the player leaves his SRS Meteor
    IF ( ( r_e_2_player_in_meteor = 1 ) AND ( NOT ( IS_CHARACTER_IN_CAR(p1, r_e_2_srs_meteor_car) ) ) )
       SET r_e_2_player_in_meteor = 0
       DISPLAY_BRIEF_NOW (2031)                            //  "I said steal a Meteor, Rooster. C'mon, boy!"
       REMOVE_ARROW (arrow1)
    ENDIF
    
    //  If the SRS Meteor has been destroyed
    IF ( ( r_e_2_player_in_meteor = 1 ) AND ( IS_CAR_WRECKED(r_e_2_srs_meteor_car) ) )
       IF ( flag_failed_redn_easy_phone2_m1 = 0 )
          CLEAR_ALL_BRIEFS ()
          DISPLAY_BRIEF_NOW (2032)                         //  "You drive like my alcoholic blind grandmother, Rooster. Without the Meteor you ain't nuthin'. Get back to the phones."
          SET flag_failed_redn_easy_phone2_m1 = 1
          DISPLAY_MESSAGE (2998)                           //  Mission Failed
          RETURN
       ENDIF
    ENDIF
    
    //  If the player entered the zaibatsu's gang zone driving a SRS Meteor
    IF ( ( IS_CHAR_IN_GANGZONE(p1, zaibgang) ) AND ( r_e_2_player_in_meteor = 1 ) )
       IF ( r_e_2_zaib_bonus_started = 0 )
          DISPLAY_BRIEF_NOW (2033)                          //  "Kill 50 of them there Zaibatsu with your SRS Meteor, Rooster!"
          r_e_2_kill_zaibatsu_bonus = START_BONUS_CHECK (zaibgang, 65535, 50, 0, CHAR, NOT_EXCLUSIVE, BY_VEHICLE, STRATOSB, zaibgang, 8)
          SET r_e_2_zaib_yet_to_kill = 50
          SET r_e_2_hunt_started = 1
          ADD_ONSCREEN_COUNTER (r_e_2_zaib_onscreen_counter, r_e_2_zaib_yet_to_kill)
          SET r_e_2_zaib_bonus_started = 1
          REMOVE_ARROW (arrow1)
       ENDIF
    ENDIF
    
    //  If the player leaves the zaibatsu's gang zone after triggering the bonus counter
    IF ( ( NOT ( IS_CHAR_IN_GANGZONE(p1, zaibgang) ) ) AND ( r_e_2_zaib_bonus_started = 1 ) )
       IF ( r_e_2_player_fit_conditions = 1 )
          DISPLAY_BRIEF_NOW (2034)                            //  "Where you goin', Rooster? Get back to the Village NOW."
          POINT_ARROW_AT (arrow1, 199.00, 25.50, 3.00)
          SET r_e_2_player_fit_conditions = 0
       ENDIF
    ENDIF
    
    //  If the player is in zaibatsu's gang zone while doing the bonus, remove the arrow and update flag
    IF ( ( IS_CHAR_IN_GANGZONE(p1, zaibgang) ) AND ( r_e_2_zaib_bonus_started = 1 ) )
       REMOVE_ARROW (arrow1)
       SET r_e_2_player_fit_conditions = 1
    ENDIF
    
    //  If the hunt has began, store the number of zaibatsu members killed in counter12
    IF ( r_e_2_hunt_started = 1 )
       STORE_BONUS_COUNT (r_e_2_kill_zaibatsu_bonus, r_e_2_zaibatsu_killed)
    ENDIF
    
    //  If the hunt has began, update the onscreen counter (the number that appears on player screen)
    IF ( r_e_2_hunt_started = 1 )
       SET r_e_2_zaib_yet_to_kill = (r_e_2_kills_goal - r_e_2_zaibatsu_killed)           //  The number of zaibatsu yet to be killed
    ENDIF
    
    //  If the player killed 50 zaibatsu members
    IF ( ( r_e_2_zaib_bonus_started = 1 ) AND ( HAS_BONUS_PASSED(r_e_2_kill_zaibatsu_bonus) ) )
       CLEAR_ALL_BRIEFS ()
       DISPLAY_BRIEF_NOW (2035)   //  "Yee-ha! We got Zaibatsu killin' Scientists and Scientists killin' Zaibatsu... You sure are The Man, Rooster! Have $30,000 fer yer fine work."
       CLEAR_ONSCREEN_COUNTER (r_e_2_zaib_onscreen_counter)
       SET r_e_2_while_2_loop = 1
    ENDIF
    
    //  If the bonus failed          ( how? I don't know )
    IF ( ( r_e_2_zaib_bonus_started = 1 ) AND ( HAS_BONUS_FAILED(r_e_2_kill_zaibatsu_bonus) ) )
       CLEAR_ALL_BRIEFS ()
       DISPLAY_BRIEF_NOW (2037)                        //  "There ain't no time limit, boy, but I'm getting' sober waitin' here!"
       CLEAR_ONSCREEN_COUNTER (r_e_2_zaib_onscreen_counter)
       SET flag_failed_redn_easy_phone2_m1 = 1
       DISPLAY_MESSAGE (2998)                          //  Mission Failed!
       RETURN
    ENDIF
 
 ENDWHILE

 //  The player passed the bonus and killed 50 zaibatsu members
 
 //  Mission Finished

 ADD_SCORE (p1, 30000)
 
 SET flag_passed_redn_easy_phone2_m1 = 1
 
 DISPLAY_MESSAGE (2999)          //  "Mission Complete!"
 
 CLEAR_WANTED_LEVEL (p1)
 
 ADD_MULTIPLIER (p1, 1)
 
 CHANGE_GANG_CHAR_RESPECT_AND_UPDATE (redngang, p1, 1)         //  RESPECT +
 
 SET_PHONE_DEAD (ph_redn_easy_phone2)                          //  Kill Redneck Easy Phone 2
 DISABLE_THREAD_TRIGGER (thr_redn_easy_phone2_m1)              //  Disable its trigger too
 
RETURN



clean_up_redn_easy_phone2_m1:        //  Clean up

	SET flag_on_mission = 0
	SET flag_on_redn_mission = 0
 
	REMOVE_ARROW (arrow1)
	REMOVE_ARROW (arrow3)
 
	CLEAR_ONSCREEN_COUNTER (r_e_2_scie_onscreen_counter)
	CLEAR_ONSCREEN_COUNTER (r_e_2_zaib_onscreen_counter)
 
 IF ( flag_passed_redn_easy_phone2_m1 = 1 )
	   ++flag_redn_missions_passed
	   ++flag_all_missions_passed
	ENDIF
 
 IF ( CHECK_DEATHARREST_EXECUTED() )
	   ++flag_redn_missions_failed
	   ++flag_all_missions_failed
	   CHANGE_GANG_CHAR_RESPECT_AND_UPDATE (redngang, p1, -1)
	   CLEAR_WANTED_LEVEL (p1)
	ENDIF
 
	MISSION_HAS_FINISHED ()
 
RETURN

MISSIONSTART
GOSUB redn_easy_phone2_m1:
GOSUB clean_up_redn_easy_phone2_m1:
MISSIONEND
